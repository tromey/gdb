# Rebuild the Tcl index. -*- tcl -*-
# To invoke:
#    tclsh make-tcl-index

# Copyright 2023 Free Software Foundation, Inc.

# This file is part of GDB.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

set index ""
# Note that this line must come first.
append index "# Tcl autoload index file, version 2.0\n"
# ... so this won't actually work.
append index "# *INDENT-OFF* THIS FILE IS GENERATED -*- buffer-read-only: t -*-\n"
append index "# vi:set ro:\n"
append index "# Copyright 2023 Free Software Foundation, Inc.\n"
append index "\n"
append index "# This file is part of GDB.\n"
append index "\n"
append index "# This program is free software; you can redistribute it and/or modify\n"
append index "# it under the terms of the GNU General Public License as published by\n"
append index "# the Free Software Foundation; either version 3 of the License, or\n"
append index "# (at your option) any later version.\n"
append index "#\n"
append index "# This program is distributed in the hope that it will be useful,\n"
append index "# but WITHOUT ANY WARRANTY; without even the implied warranty of\n"
append index "# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n"
append index "# GNU General Public License for more details.\n"
append index "#\n"
append index "# You should have received a copy of the GNU General Public License\n"
append index "# along with this program.  If not, see <http://www.gnu.org/licenses/>.\n"
append index "\n"
append index "# Use make-tcl-index to regenerate\n\n"

array set seen {}

proc add_entry {name file} {
    if {[info exists ::seen($name)]} {
	# A duplicate is ok as long as the officially recorded
	# location is gdb.exp or mi-support.exp.  This is a big hack,
	# but needed for now due to the proc rewriting in
	# gdbserver-support.exp.
	if {$::seen($name) != $file && $::seen($name) != "gdb.exp"
	    && $::seen($name) != "mi-support.exp"} {
	    puts "Mismatch for $name: $::seen($name) != $file"
	}
    } else {
	set ::seen($name) $file
	append ::index "set [list auto_index($name)]"
	append ::index " \[list source \[file join \$dir [list $file]\]\]\n"
    }
}

# Sigh.
proc move_to_front {a_list elt} {
    set index [lsearch -exact $a_list $elt]
    if {$index == -1} {
	puts "could not find $elt"
	exit 1
    }
    set a_list [lreplace $a_list $index $index]
    return [linsert $a_list 0 $elt]
}

cd lib

# See the comment in add_entry to understand this ordering.
set files [lsort [glob -- *.exp *.tcl]]
set files [move_to_front $files mi-support.exp]
set files [move_to_front $files gdb.exp]

foreach file $files {
    set f [open $file]
    set ns_list {}
    while {[gets $f line] >= 0} {
	switch -regexp -matchvar match $line {
	    "^[[:space:]]*(?:proc|gdb_caching_proc|proc_with_prefix) ([-a-z0-9A-Z_:@]+)" {
		set nlist $ns_list
		lappend nlist [lindex $match 1]
		set name [auto_qualify [join $nlist ::] ::]
		add_entry $name $file
	    }
	    "^[[:space:]]*namespace ensemble create -command ::([a-z0-9A-Z_]+)" {
		set name [lindex $match 1]
		add_entry $name $file
	    }
	    "^namespace eval ([a-z0-9A-Z_:]+)" {
		lappend ns_list [lindex $match 1]
	    }
	    "^[[:space:]]*namespace export (.+)$" {
		foreach name [split [lindex $match 1]] {
		    set nlist $ns_list
		    lappend nlist $name
		    set name [auto_qualify [join $nlist ::] ::]
		    add_entry $name $file
		}
	    }
	    "^\}" {
		set ns_list [lreplace $ns_list end end]
	    }
	}
    }
    close $f
}

set f [open tclIndex w]
puts -nonewline $f $index
close $f
