# Copyright 2025-2026 Free Software Foundation, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Check how GDB handles a core file which appears to have no threads
# within it.  We do this by asking the kernel to create a "normal"
# core file, then use a Python script modify the core file, hiding the
# NT_PRSTATUS notes.  The NT_PRSTATUS notes contain the general
# purpose registers for each thread, so with these gone GDB will
# believe the core file has no threads.
#
# In this situation, GDB should create a single dummy thread.  There's
# not much a user can do to debug such a core file, with no
# NT_PRSTATUS the general registers will be missing, which means no
# $pc value, no backtrace, etc.
#
# The critical thing though, is that GDB doesn't crash.  That would be
# bad.

require allow_python_tests

standard_testfile

set pyfile [gdb_remote_download host ${srcdir}/${subdir}/${testfile}.py]

if {[build_executable "build executable" $testfile $srcfile] == -1} {
    return
}

set generated_core [core_find $binfile]
if {$generated_core == ""} {
    untested "unable to create corefile"
    return
}
set corefile [standard_output_file $testfile.core]
remote_exec build "mv $generated_core $corefile"

# Now try loading the core file.
clean_restart

# Load the Python script, and run the command which modifies the core
# file.
gdb_test_no_output "source $::pyfile" "import python scripts"
gdb_test "modify-core-file \"$corefile\" 0x1" ".*" \
    "update core file"

set saw_no_regs_warning false
set saw_generated_by_line false
gdb_test_multiple "core-file $corefile" "load core file" {
    -re "Core was generated by `\[^\r\n\]+$testfile'\\.\r\n" {
	set saw_generated_by_line true
	exp_continue
    }

    -re "^warning: Couldn't find general-purpose registers in core file\\.\r\n" {
	set saw_no_regs_warning true
	exp_continue
    }

    -re "^$gdb_prompt $" {
	gdb_assert { $saw_no_regs_warning && $saw_generated_by_line } \
	    $gdb_test_name
    }

    -re "^\[^\r\n\]+\r\n" {
	exp_continue
    }
}

gdb_test "p 1 + 1" " = 2" "gdb is still alive"
