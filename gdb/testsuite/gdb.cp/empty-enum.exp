# Copyright 2023-2026 Free Software Foundation, Inc.
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Test how GDB displays empty enums.  At one point an enum with no
# enumeration values would be considered a flag enum, and, as a
# consequence any value with that type would display like:
#
#   (gdb) print enum_var
#   $1 = (unknown: 0x8)
#
# Which resulted in a lot of noise.  Now GDB treats empty enums as a
# non-flag enum, and should print them like this:
#
#   (gdb) print enum_var
#   $1 = 8
#
# This test checks this behavior.

standard_testfile .cc

set opts {}
lappend opts debug
lappend opts c++
lappend opts additional_flags=-std=c++11

if {[prepare_for_testing "failed to prepare" $testfile $srcfile $opts]} {
    return -1
}

if {![runto_main]} {
    return -1
}

gdb_breakpoint "breakpt"
gdb_continue_to_breakpoint "stop in breakpt"


gdb_test "print arg1" " = 8"
gdb_test "print arg2" " = 4"


# Xfail for missing DW_AT_type in DW_TAG_enumeration_type, gcc PR debug/16063.
set have_xfail [expr {[test_compiler_info gcc-*] && [gcc_major_version] < 5}]

set enum1 [multi_line "" \
    "type = enum enum1 : unsigned int {" \
    "    <no enum values>" \
    "}"]
set enum1_no_type [multi_line "" \
    "type = enum enum1 {" \
    "    <no enum values>" \
    "}"]
gdb_test_multiple "ptype arg1" "" {
    -re -wrap $enum1 {
	pass $gdb_test_name
    }
    -re -wrap $enum1_no_type {
	if { $have_xfail } {
	    setup_xfail *-*-* gcc/16063
	}
	fail $gdb_test_name
    }
}

set enum2 [multi_line "" \
    "type = enum class enum2 : unsigned char {" \
    "    <no enum values>" \
    "}"]
set enum2_no_type [multi_line "" \
    "type = enum class enum2 {" \
    "    <no enum values>" \
    "}"]
gdb_test_multiple "ptype arg2" "" {
    -re -wrap $enum2 {
	pass $gdb_test_name
    }
    -re -wrap $enum2_no_type {
	if { $have_xfail } {
	    setup_xfail *-*-* gcc/16063
	}
	fail $gdb_test_name
    }
}
